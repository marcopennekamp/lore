-- Objective: Transcribe DNA to RNA.
module lessons.rna

use lore.list.map
use lore.string.concat_map
use lore.symbol.Symbol

type DnaNucleotide = #a | #t | #g | #c
type RnaNucleotide = #a | #u | #g | #c

type Dna = [DnaNucleotide]
type Rna = [RnaNucleotide]

func transcribe(DnaNucleotide): RnaNucleotide
func transcribe(#a): #u = #u
func transcribe(#t): #a = #a
func transcribe(#g): #c = #c
func transcribe(#c): #g = #g

func transcribe(dna: Dna): Rna = dna.map(transcribe)

func stringify(rna: Rna): String = rna.concat_map(Symbol.name)

func main(): [String] =
  let cases = [
    [#a],
    [#t],
    [#g],
    [#c],
    [#a, #c, #g, #t, #g, #g, #t, #c, #t, #t, #a, #a],
  ]
  cases |> map(transcribe) |> map(stringify)
