/**
 * All combat stats for fighting entities.
 *
 * Note that you should use the accompanying base_stat function to access all stats except health, which may be accessed
 * directly. The reason is that select entities may want to overwrite the stats without changing the Stats object.
 */
struct Stats
  // TODO: Does this really belong here?
  mut health: Int

  /**
   * The current health stat cannot surpass this maximum value.
   */
  maximum_health: Int

  /**
   * Regeneration determines how much health the combatant regenerates at the end of each combat turn.
   */
  regeneration: Int = 0

  /**
   * Damage reduction, point-by-point.
   */
  defense: Int = 0

  /**
   * Dodge reduces damage received for every nth hit. A dodge of 0 means that the entity cannot dodge, while a value of
   * 0.5 means that the entity will dodge every second hit, 0.25 every fourth hit, and so on.
   *
   * Note that dodge would usually be implemented with an element of chance, but this would not lead to consistent
   * test results.
   */
  dodge: Real = 0.0

  /**
   * Attack determines the regular damage done by an attack.
   */
  attack: Int = 0

  /**
   * The range of the attack. A range of 0 means melee distance, while each successive increment allows the combatant
   * to hit their enemy earlier. In most combat situations, opponents start 6-8 points apart.
   */
  range: Int = 0

  /**
   * The running speed of the entity, per turn. This is used to close the distance to an opponent with ranged attacks.
   */
  speed: Int = 1
end

type +Stats = %{ stats: Stats }

/**
  * A stat label uniquely names a specific stat in the Stats struct.
  */
type StatLabel = StatLabelInt | StatLabelReal
type StatLabelInt = #regeneration | #defense | #attack | #range | #speed
type StatLabelReal = #dodge

domain entity: +Stats
  func alive?(): Boolean = entity.stats.health > 0

  func base_stat(StatLabel): Int | Real
  func base_stat(StatLabelInt): Int

  func base_stat(#regeneration): Int = entity.stats.regeneration
  func base_stat(#defense): Int = entity.stats.defense
  func base_stat(#attack): Int = entity.stats.attack
  func base_stat(#range): Int = entity.stats.range
  func base_stat(#speed): Int = entity.stats.speed
  func base_stat(#dodge): Real = entity.stats.dodge
end
