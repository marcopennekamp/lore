module geometry.comparison

use lore.Enum._
use lore.test._

use geometry._

@root
module lore.core do
  func less_than?(s1: Shape, s2: Shape): Boolean = area(s1) < area(s2)
  func less_than_equal?(s1: Shape, s2: Shape): Boolean = area(s1) <= area(s2)
end

func check(s1: Shape, s2: Shape): [String] = do
  let mut result: [String] = []

  // TODO: This could be replaced with a list construction that contains options via option ifs and then the list gets
  //       flattened: `[if s1 < s2 then '...', if s1 <= s2 then '...'] |> flatten`.
  if s1 < s2
    result = result :+ '${name(s1)} < ${name(s2)}'
  end
  if s1 <= s2
    result = result :+ '${name(s1)} <= ${name(s2)}'
  end
  if s2 < s1
    result = result :+ '${name(s2)} < ${name(s1)}'
  end
  if s2 <= s1
    result = result :+ '${name(s2)} <= ${name(s1)}'
  end

  result
end

func main(): [String] = do
  let rect = Rectangle(0.0, 0.0, 6.0, 8.0)
  let box = BoundingBox(2.0, 10.0, 8.0, 14.0)
  let circle = Circle(0.0, 0.0, 5.0)

  [
    check(rect, box),
    check(rect, circle),
    check(box, circle),
  ] |> flatten
end

@root
module geometry do
  spec 'comparison: shape comparisons result in the correct relations between shapes' do
    assert_equal(
      main(),
      ['rect <= box', 'box <= rect', 'rect < circle', 'rect <= circle', 'box < circle', 'box <= circle'],
    )
  end
end
